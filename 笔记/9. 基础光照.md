# 基础光照

- 基础知识

  - 冯氏光照模型

    ```
    冯氏光照模型是对现实光照在计算机进行模拟的模型，该模型重要由三个分量决定：
    1.环境光照(Ambient)
      即使在黑暗的情况下，世界上通常也仍然有一些光亮（月亮、远处的光），所以物体几乎永远不会是完全黑暗的。   为了模拟这个，我们会使用一个环境光照常量，它永远会给物体一些颜色。
    2.漫反射光照(Diffuse) 
      模拟光源对物体的方向性影响(Directional Impact)。它是冯氏光照模型中视觉上最显著的分量。物体的某   一部分越是正对着光源，它就会越亮。 
    3.镜面(Specular)光照
      模拟有光泽物体上面出现的亮点。镜面光照的颜色相比于物体的颜色会更倾向于光的颜色。
    ```

    ![](C:\Users\曾伟\Desktop\typora笔记\计算机图形学笔记\笔记\assets\phong.png)





- 实验

1. 环境光照

   ```
   由于环境光照作用是强化或者淡化发光源发出的光投射在物体上，因此只要在反射回来的颜色值乘以发光源的颜色值即可
   ```

   ```c++
   #version 330 core
   out vec4 FragColor;  
   uniform vec3 objectColor;
   uniform vec3 lightColor;
   void main()
   {
       float ambientStrength = 0.1;     //模拟在黑暗当中的物体
       vec3 ambient = ambientStrength * lightColor;
       vec3 result = ambient * objectColor;
       FragColor = vec4(result, 1.0f);
   }
   ```

2. 漫反射光照

   ```
   漫反射对物体的视觉影响较大，在漫反射阶段，决定了物体的明暗。漫反射光照使物体上与光线方向越接近的片段能从光源处获得更多的亮度。为了能够更好的理解漫反射光照，请看下图 ：
   ```

   ![](C:\Users\曾伟\Desktop\typora笔记\计算机图形学笔记\笔记\assets\diffuse.png)

   灯光照射到物体上的明暗程度取决于光线与反射面发现之间的夹角，夹角约小该点越亮。接下来的就是确定夹角大小，两向量点乘可以知道夹角----==余弦值==；接下来就是如何确定法向量以及定向光线。

   - 法向量

     由于本例子当中物体是规则的立方体，法向量很容易计算，我们直接手动加上去，如果是不规则的物体我们仍然要用叉乘的方法去获取每个顶点的法向量。

     ```
     float vertices[] = {
         -0.5f, -0.5f, -0.5f,  0.0f,  0.0f, -1.0f,
          0.5f, -0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 
          0.5f,  0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 
          0.5f,  0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 
         -0.5f,  0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 
         -0.5f, -0.5f, -0.5f,  0.0f,  0.0f, -1.0f, 
     
         -0.5f, -0.5f,  0.5f,  0.0f,  0.0f, 1.0f,
          0.5f, -0.5f,  0.5f,  0.0f,  0.0f, 1.0f,
          0.5f,  0.5f,  0.5f,  0.0f,  0.0f, 1.0f,
          0.5f,  0.5f,  0.5f,  0.0f,  0.0f, 1.0f,
         -0.5f,  0.5f,  0.5f,  0.0f,  0.0f, 1.0f,
         -0.5f, -0.5f,  0.5f,  0.0f,  0.0f, 1.0f,
     
         -0.5f,  0.5f,  0.5f, -1.0f,  0.0f,  0.0f,
         -0.5f,  0.5f, -0.5f, -1.0f,  0.0f,  0.0f,
         -0.5f, -0.5f, -0.5f, -1.0f,  0.0f,  0.0f,
         -0.5f, -0.5f, -0.5f, -1.0f,  0.0f,  0.0f,
         -0.5f, -0.5f,  0.5f, -1.0f,  0.0f,  0.0f,
         -0.5f,  0.5f,  0.5f, -1.0f,  0.0f,  0.0f,
     
          0.5f,  0.5f,  0.5f,  1.0f,  0.0f,  0.0f,
          0.5f,  0.5f, -0.5f,  1.0f,  0.0f,  0.0f,
          0.5f, -0.5f, -0.5f,  1.0f,  0.0f,  0.0f,
          0.5f, -0.5f, -0.5f,  1.0f,  0.0f,  0.0f,
          0.5f, -0.5f,  0.5f,  1.0f,  0.0f,  0.0f,
          0.5f,  0.5f,  0.5f,  1.0f,  0.0f,  0.0f,
     
         -0.5f, -0.5f, -0.5f,  0.0f, -1.0f,  0.0f,
          0.5f, -0.5f, -0.5f,  0.0f, -1.0f,  0.0f,
          0.5f, -0.5f,  0.5f,  0.0f, -1.0f,  0.0f,
          0.5f, -0.5f,  0.5f,  0.0f, -1.0f,  0.0f,
         -0.5f, -0.5f,  0.5f,  0.0f, -1.0f,  0.0f,
         -0.5f, -0.5f, -0.5f,  0.0f, -1.0f,  0.0f,
     
         -0.5f,  0.5f, -0.5f,  0.0f,  1.0f,  0.0f,
          0.5f,  0.5f, -0.5f,  0.0f,  1.0f,  0.0f,
          0.5f,  0.5f,  0.5f,  0.0f,  1.0f,  0.0f,
          0.5f,  0.5f,  0.5f,  0.0f,  1.0f,  0.0f,
         -0.5f,  0.5f,  0.5f,  0.0f,  1.0f,  0.0f,
         -0.5f,  0.5f, -0.5f,  0.0f,  1.0f,  0.0f
     };
     ```

     更新了顶点数据之后，我们也要相应更新顶点着色器以及更新对顶点数据的解释

     - 更新被光照的物体顶点着色器

       ```c++
       #version 330 core
       layout (location = 0) in vec3 aPos;   // 位置变量的属性位置值为 0 
       layout (location = 1) in vec3 aNormal;
       uniform mat4 model;
       uniform mat4 view;
       uniform mat4 projection;
       out vec3 Normal;
       out vec3 FragPos;  
       void main()
       {
           gl_Position =projection * view * model * vec4(aPos, 1.0);  //顺序必须正确！
           FragPos = vec3(model * vec4(aPos, 1.0));
           Normal = mat3(transpose(inverse(model))) * aNormal;   //防止物体进行不等比放缩时法向量不垂直顶点
       }
       //mat3(transpose(inverse(model)))把法向量坐标转换成世界坐标系当中去，主要解决的是当物体进行不等比放缩时各个顶点的法向量依然能垂直于相关的平面。transpose(inverse(model))为法向量矩阵
       ```

     - 更新被光照的物体片段着色器

       ```c++
       #version 330 core
       out vec4 FragColor;  
       uniform vec3 objectColor;
       uniform vec3 lightColor;
       uniform vec3 lightPos;    //光源位置方便计算定向光线的方向
       in vec3 Normal;
       in vec3 FragPos;
       void main()
       {
           float ambientStrength = 0.1;
           vec3 ambient = ambientStrength * lightColor;
           //添加光照计算
           vec3 norm = normalize(Normal);//将法向量进行单位化
           vec3 lightDir = normalize(lightPos - FragPos);//计算光线的方向，指向光源的向量
           //向量准备完毕，进行计算夹角大小----点乘
           float diff = max(dot(norm, lightDir), 0.0);   //保证夹角大于等于0 ,否则出现奇怪的反射现象
           vec3 diffuse = diff * lightColor;
           vec3 result = (ambient + diffuse) * objectColor;//将环境光照与漫反射光照向量进行加并乘以物体颜色。
           FragColor = vec4(result, 1.0);
       }
       ```

     - 修改物体的位置属性并加入法向量数据解读，同时不需要修改灯光的位置属性

       ```c++
       //位置属性
           glVertexAttribPointer(0, 3, GL_FLOAT, GL_FALSE, 6 * sizeof(float), (void*)0);
           glEnableVertexAttribArray(0);
           glVertexAttribPointer(1, 3, GL_FLOAT, GL_FALSE, 6 * sizeof(float), (void*)(3 * sizeof(float)));
           glEnableVertexAttribArray(1);
       ```

     - 加入把灯光位置传入物体的片段着色器方便计算光线的方向

       ```c++
        myshader.setVec3("lightPos", lightPos);
       ```

       

   

3. 镜面光照

  ​                                       ![](C:\Users\曾伟\Desktop\typora笔记\计算机图形学笔记\笔记\assets\specular.png)

```
简介：
    我们通过反射法向量周围光的方向来计算反射向量。然后我们计算反射向量和视线方向的角度差，如果夹角越小，那么镜面光的影响就会越大。它的作用效果就是，当我们去看光被物体所反射的那个方向的时候，我们会看到一个高光。
```

- 辅助工具：

  1. 法向量
  2. 光线的方向向量
  3. 观察方向（观察向量）：我们可以使用观察者世界空间位置和片段的位置来计算它，之后，我们计算镜面光强度，用它乘以光源的颜色，再将它加上环境光和漫反射分量

  ==实验备注：==我们在世界坐标系当中进行计算而不是观察者坐标系。

- 实验步骤

  1. 由于辅助工具当中的1，2我们都已经具备，我们仅仅计算3。传入摄像机位置坐标的方向来当作观察视角（观察向量）,因此传入摄像机位置到物体片段着色器：

     ```c++
     uniform vec3 viewPos;     //摄像机位置
     ```

     ```c++
      myshader.setVec3("viewPos", camera.Position);//main主函数
     ```

  2. 计算高光强度

     首先，我们定义一个==镜面强度(Specular Intensity)==变量，给镜面高光一个中等亮度颜色，让它不要产生过度的影响。

     ```c++
     float specularStrength = 0.5;
     ```

     下一步，我们计算视线方向向量，和对应的沿着法线轴的反射向量：

     ```c++
     vec3 viewDir = normalize(viewPos - FragPos);//视线方向，指向眼睛的向量
     vec3 reflectDir = reflect(-lightDir, norm); //反射向量
     //需要注意的是我们对lightDir向量进行了取反。reflect函数要求第一个向量是从光源指向片段位置的向量，但是lightDir当前正好相反，是从片段指向光源（由先前我们计算lightDir向量时，减法的顺序决定）。为了保证我们得到正确的reflect向量，我们通过对lightDir向量取反来获得相反的方向。第二个参数要求是一个法向量，所以我们提供的是已标准化的norm向量。
     ```



​              再下一步，计算镜面分量

```c++
 //我们先计算视线方向与反射方向的点乘（并确保它不是负值），然后取它的32次幂。这个32是高光的反光度(Shininess)。一个物体的反光度越高，反射光的能力越强，散射得越少，高光点就会越小
 float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32);
 //计算镜面分量
 vec3 specular = specularStrength * spec * lightColor;
```
 最后一步，把镜面分量加入冯氏光照模型当中去


 ```c++
 vec3 result = (ambient + diffuse + specular) * objectColor;
 FragColor = vec4(result, 1.0);
 ```

```c++
// 完整的片段着色器代码
 #version 330 core
 out vec4 FragColor;  
 uniform vec3 objectColor;
 uniform vec3 lightColor;
 uniform vec3 lightPos;    //光源位置方便计算定向光线的方向
 uniform vec3 viewPos;     //摄像机位置
 in vec3 Normal;
 in vec3 FragPos;
 void main()
 {
    //ambient
     float ambientStrength = 0.1;           //设置环境反射的强度
     vec3 ambient = ambientStrength * lightColor;
 
   //diffuse 
     //添加光照计算
     vec3 norm = normalize(Normal);//将法向量进行单位化
     vec3 lightDir = normalize(lightPos - FragPos);//计算光线的方向，该方向指向灯光
     vec3 viewDir = normalize(viewPos - FragPos);  //视角向量，该方向指向摄像机
     //向量准备完毕，进行计算夹角大小----点乘
     float diff = max(dot(norm, lightDir), 0.0);   //保证夹角大于等于0 ,否则出现奇怪的反射现象
     vec3 dif fuse = diff * lightColor;
   //specular
     //计算夹角
     float specularstrength= 0.5;           //设置镜面反射的强度
     vec3 reflectDir = reflect(-lightDir, norm);    //计算夹角，reflect函数要求第一个向量是从光源指向片段位置的向量。
     //向量准备完毕，计算镜面分量
     float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32);
     vec3 specular = specularstrength * spec * lightColor;
   
 
     vec3 result = (ambient + diffuse+specular) * objectColor;//将环境光照与漫反射光照向量进行加并乘以物体颜色。
     FragColor = vec4(result, 1.0);
 }
```

- 实验结果 

  ![](C:\Users\曾伟\Desktop\typora笔记\计算机图形学笔记\笔记\assets\result.png)

[源代码](C:\Users\曾伟\Desktop\typora笔记\计算机图形学笔记\源代码\基础光照源代码.md)（按住Ctrl点击鼠标左键）

